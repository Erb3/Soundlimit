plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
	id "com.modrinth.minotaur" version "2.+"
}

version = "${project.mod_version}-${project.minecraft_version}"

repositories {}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
}

processResources {
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
			"minecraft_version": project.minecraft_version,
			fabric_loader_version: project.fabric_loader_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	def javaVersion = JavaVersion.toVersion(21)

	if (JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(21)
	}

	archivesBaseName = project.archives_base_name
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "UIXLYpkp"
	versionNumber = project.mod_version
	versionName = "${project.mod_version} for ${project.minecraft_version}"
	versionType = "release"
	changelog = "Automatically uploaded with Minotaur. Changelog available on [GitHub releases](https://github.com/Erb3/Soundlimit/releases/tag/${project.mod_version}-${project.minecraft_version})."
	gameVersions = [project.minecraft_version]
	loaders = ["fabric"]
	uploadFile = remapJar
	syncBodyFrom = rootProject.file("README.md").text

	dependencies {}
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
